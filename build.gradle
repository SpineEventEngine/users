//
// Copyright 2019, TeamDev. All rights reserved.
//
// Redistribution and use in source and/or binary forms, with or without
// modification, must retain the above copyright notice and the following
// disclaimer.
//
// THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
// "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
// LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
// A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
// OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
// SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
// LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
// DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
// THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
// (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
// OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
//

buildscript { final scriptHandler ->
    apply from: "$rootDir/version.gradle"
    apply from: "$rootDir/config/gradle/dependencies.gradle"

    defaultRepositories(scriptHandler)
    dependencies {
        classpath deps.build.guava
        classpath deps.build.gradlePlugins.errorProne
        classpath deps.build.gradlePlugins.protobuf
        classpath "io.spine.tools:spine-model-compiler:$spineBaseVersion"
    }

    configurations.all {
        resolutionStrategy {
            force(
                    "io.spine:spine-base:$spineBaseVersion",
                    "io.spine.tools:spine-plugin-base:$spineBaseVersion",
                    "io.spine.tools:spine-model-compiler:$spineBaseVersion"
            )
        }
    }
    forceConfiguration(scriptHandler)
}

allprojects {
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'idea'

    group = 'io.spine'
    version = versionToPublish

    allprojects {
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
            }
        }
    }
}

ext {
    projectsToPublish = ["client", "server"]
}

subprojects {

    configurations.all {
        resolutionStrategy {
            force(
                    "io.spine:spine-base:$spineBaseVersion"
            )
        }
    }

    forceConfiguration(project)
    configurations {
        // Avoid collisions of Java classes defined both in `protobuf-lite` and `protobuf-java`
        runtime.exclude group: "com.google.protobuf", module: "protobuf-lite"
        testRuntime.exclude group: "com.google.protobuf", module: "protobuf-lite"
    }

    project.ext {
        sourcesRootDir = "$projectDir/src"
        generatedRootDir = "$projectDir/generated"

        generatedJavaDir = "$generatedRootDir/main/java"
        generatedTestJavaDir = "$generatedRootDir/test/java"

        generatedGrpcDir = "$generatedRootDir/main/grpc"
        generatedTestGrpcDir = "$generatedRootDir/test/grpc"

        generatedSpineDir = "$generatedRootDir/main/spine"
        generatedTestSpineDir = "$generatedRootDir/test/spine"

        spineProtobufPluginId = 'io.spine.tools.spine-model-compiler'
    }

    apply plugin: 'com.google.protobuf'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: spineProtobufPluginId
    apply plugin: 'net.ltgt.errorprone'
    apply plugin: 'pmd'

    apply from: deps.scripts.javacArgs
    apply from: deps.scripts.pmd
    apply from: deps.scripts.modelCompiler
    apply from: deps.scripts.projectLicenseReport

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    defaultRepositories(project)

    dependencies {
        errorprone deps.build.errorProneCore
        errorproneJavac deps.build.errorProneJavac
        // For dependencies config. based on version of Java, see:
        //  https://github.com/epeee/junit-jupiter-extension-testing/blob/57b7ba75ab64ed8c229d2a5b14a954d6ae359189/gradle/errorprone.gradle

        implementation deps.build.guava
        implementation deps.build.checkerAnnotations
        implementation deps.build.errorProneAnnotations

        compile group: 'io.spine', name: 'spine-server', version: spineVersion

        testImplementation deps.test.junit5Api
        testImplementation deps.test.junit5Runner

        // Depend on Truth directly to avoid the following error when running tests:
        // java.lang.NoClassDefFoundError: Could not initialize class com.google.common.truth.Truth
        //      The `Truth` class is a part of `com.google.truth:truth` artifact.
        // This artifact IS provided transitively from `io.spine:spine-testlib` via `api`
        // dependency. `spine-testlib` in tern is the `api` dependency of `testutil-core`, which is
        // `api` dependency of `spine-testutil-client`, which is `api` dependency of
        // `spine-testutil-server` on which we depend below.
        //      Thus, the `Truth` library being transitive `api` dependency should be available
        // on run-time, but it is not. This could be a Gradle issue, but assuming other things
        // to be done in this library, it is more practical to add the missing dependency and
        // address this "mystery" when time permits.
        testImplementation deps.test.truth
        testImplementation("io.spine:spine-testutil-server:${spineVersion}") {
            // We are using JUnit 5.x instead of 4.x dependency of Spine testing framework.
            exclude group: 'junit'
        }
    }

    sourceSets {
        main {
            proto.srcDirs "$sourcesRootDir/main/proto"
            java.srcDirs generatedJavaDir, "$sourcesRootDir/main/java", generatedSpineDir
            resources.srcDirs "$generatedRootDir/main/resources"
        }
        test {
            proto.srcDirs "$sourcesRootDir/test/proto"
            java.srcDirs generatedTestJavaDir, "$sourcesRootDir/test/java", generatedTestSpineDir
            resources.srcDirs "$generatedRootDir/test/resources"
        }
    }

    /* Uncomment this block if you need to display console output during the Gradle build.*/
    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    apply from: deps.scripts.testOutput
    apply from: deps.scripts.javadocOptions

    javadoc {
        source = sourceSets.main.allJava
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier.set("sources")
    }

    task testOutputJar(type: Jar) {
        from sourceSets.test.output
        archiveClassifier.set("test")
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        from("$projectDir/build/docs/javadoc")
        archiveClassifier.set("javadoc")
    }

    idea.module.iml {
        beforeMerged { final module ->
            module.dependencies.clear()
        }
        whenMerged { final module ->
            module.dependencies*.exported = true
        }
    }

    idea.module {
        generatedSourceDirs += file(generatedJavaDir)
        testSourceDirs += file(generatedTestJavaDir)
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

idea.project.ipr {
    beforeMerged { final project ->
        project.modulePaths.clear()
    }
    withXml { final provider ->
        provider.node.component
                .find { it.@name == 'VcsDirectoryMappings' }
                .mapping.@vcs = 'Git'
    }
}

apply from: deps.scripts.jacoco
apply from: deps.scripts.publish
apply from: deps.scripts.repoLicenseReport
apply from: deps.scripts.generatePom
